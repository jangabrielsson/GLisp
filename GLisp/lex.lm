/*
//
//  Copyright (c) 2012, Jan Gabrielsson
//  All rights reserved.
//
//  Redistribution and use in source and binary forms, with or without
//  modification, are permitted provided that the following conditions are met:
//
//  1. Redistributions of source code must retain the above copyright notice, this
//  list of conditions and the following disclaimer.
//  2. Redistributions in binary form must reproduce the above copyright notice,
//  this list of conditions and the following disclaimer in the documentation
//  and/or other materials provided with the distribution.
//  
//  THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS" AND
//  ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED
//  WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
//  DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT OWNER OR CONTRIBUTORS BE LIABLE FOR
//  ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES
//  (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES;
//   LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
//  ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT
//  (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
//  SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
//  
//  The views and conclusions contained in the software and documentation are those
//  of the authors and should not be interpreted as representing official policies,
//  either expressed or implied, of the FreeBSD Project.
//
 */

%option yylineno
%option noyywrap

%{
#ifndef TRUE
#define TRUE 1
#endif
    
#ifndef FALSE
#define FALSE 0
#endif
    
#include <stdio.h>
#include <ctype.h>
#include <string.h>
#include "y.tab.h"
    
    int badtoken_cnt = 0;
    int token_cnt = 0;
    int col_cnt = 0;
    int lineno = 0;

    char lexBuffer[8192];
    static long lexBuffPtr = 0;
    
#define CLEARBUFF() (lexBuffer[lexBuffPtr=0]=0)
#define APPENDCHARTOBUFF(CH) {lexBuffer[lexBuffPtr++]=CH; lexBuffer[lexBuffPtr]=0;}
#define COPYSTRINGTOBUFF(STR) {strcpy(lexBuffer,STR); lexBuffPtr=strlen(STR);}
#define APPENDSTRINGTOBUFF(STR) { for(int i = 0; STR[i];) lexBuffer[lexBuffPtr++] = STR[i++]; lexBuffer[lexBuffPtr]=0;}

#undef YY_INPUT
#define YY_INPUT(b,r,s) readInputForLexer(b,&r,s)

%}

%x string
%x symbol

specquoted  (\\,|\\\.|\\`|\\'|\\&|\\#)
comment     \/\*([^*]|\n)*\*\/
digit       [0-9]
ichar       [A-Z_a-z_*@]
integer     [+-]?{digit}+
newline     \n
strchar     ([ ~]|\\n)
identifier  ({ichar}|{specquoted})+([0-9_-]|{ichar}|{specquoted})*
keyword     [&]{ichar}([0-9]|{ichar})*
message     {ichar}([0-9]|{ichar})*[:]
whitespace  [ \t]+
float       ([+-]?{digit}+)?\.{digit}*(e?[+-]?{digit}+)?
chrliteral  '([!*]|\\n)'
nullstring  \"\"
escquote    [^"]*\\\"[^"]*
strliteral  \"[^"]*{escquote}*\"
str2literal \"(\\.|[^\\"])*\"

%%

"#"             { COPYSTRINGTOBUFF(yytext); return tFUNCTION;}
"`"             { COPYSTRINGTOBUFF(yytext); return tBACK_QUOTE;}
"'"             { COPYSTRINGTOBUFF(yytext); return tQUOTE;}
"("             { COPYSTRINGTOBUFF(yytext); return tLBRACE;}
")"             { COPYSTRINGTOBUFF(yytext); return tRBRACE;}
"["             { COPYSTRINGTOBUFF(yytext); return tLBRACK;}
"]"             { COPYSTRINGTOBUFF(yytext); return tRBRACK;}
"."             { COPYSTRINGTOBUFF(yytext); return tDOT;}
">"             { COPYSTRINGTOBUFF(yytext); return tGREATER;}
"<"             { COPYSTRINGTOBUFF(yytext); return tLESS;}
"="             { COPYSTRINGTOBUFF(yytext); return tEQUAL;}
"+"             { COPYSTRINGTOBUFF(yytext); return tADD;}
"-"             { COPYSTRINGTOBUFF(yytext); return tSUB;}
"*"             { COPYSTRINGTOBUFF(yytext); return tMUL;}
"/"             { COPYSTRINGTOBUFF(yytext); return tDIV;}
","             { COPYSTRINGTOBUFF(yytext); return tCOMMA;}
",."            { COPYSTRINGTOBUFF(yytext); return tCOM_DOT;}
",@"            { COPYSTRINGTOBUFF(yytext); return tCOM_AT;}

{float}         { COPYSTRINGTOBUFF(yytext); return tFLOAT; }
{integer}       { COPYSTRINGTOBUFF(yytext); return tINTEGER; }
{chrliteral}    { COPYSTRINGTOBUFF(yytext); return tCHRLIT; }

{identifier}    { COPYSTRINGTOBUFF(yytext); return tATOM; }

{keyword}       { COPYSTRINGTOBUFF(yytext); return tKEYWORD; }
{message}       { COPYSTRINGTOBUFF(yytext); return tMESSAGE; }
{newline}       { col_cnt = 1; }
{whitespace}    { col_cnt+=yyleng; }
{comment}       { col_cnt = 0; }

\"                  { BEGIN string; CLEARBUFF();}
<string>[^\\"\n]*   { APPENDSTRINGTOBUFF(yytext); }
<string>\\n         { APPENDCHARTOBUFF('\n'); }
<string>\\\(        { APPENDCHARTOBUFF('('); }
<string>\\t         { APPENDCHARTOBUFF('\t'); }
<string>\\[0-7]*    { APPENDCHARTOBUFF(strtol(yytext+1, 0, 8)); }
<string>\\[\\"]     { APPENDCHARTOBUFF(yytext[1]); }
<string>\"          { BEGIN 0; return tSTRLIT; }
<string>\\.         { printf("bogus escape '%s' in string\n", yytext); return tERROR; }
<string>\n          { APPENDCHARTOBUFF('\n'); }

"//"            { /* handle C++ style comments */
    char c;
    do { c = input();
    } while (c != '\n');
    lineno++;
}

";"            { /* handle ;;; style comments */
    char c;
    do { c = input();
    } while (c != '\n');
    lineno++;
}

.               { printf("Unrecognized token '%s'\n", yytext); return tERROR;}

%%
